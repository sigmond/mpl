set verbose 0
set debug 0

proc reset_passes_and_fails {} {
    global passes
    global fails
    set passes 0
    set fails 0
}

proc show_passes_and_fails {} {
    global passes
    global fails
    send_user "PASSED: $passes, FAILED: $fails\n"
}

proc pass { msg } {
    global passes
    #send_user "PASS: $msg\n"
    incr passes
}

proc fail { msg } {
    global fails
    #send_user "\nFAIL: $msg\n"
    incr fails
}

# convert integer to ascii char
proc asc i {
    if { $i<0 || $i>255 } { error "asc:Integer out of range 0-255" }
    return [format %c $i ]
}

proc chr c {
    if {[string length $c] > 1 } { error "chr: arg should be a single char"}
#   set c [ string range $c 0 0]
    set v 0;
    scan $c %c v; return $v
}


proc convert_decimal_to_string {decimal_value} {
  set hex_el [format %.8x [expr {$decimal_value & 0xFFFFFFFF}]]
  set reversed_hex_el [string reverse $hex_el]
  set amended_reversed_hex_el_byte1 [string reverse [string replace $reversed_hex_el 2 7]]
  set amended_reversed_hex_el_byte2 [string reverse [string replace [string replace $reversed_hex_el 0 1] 2 5]]
  set amended_reversed_hex_el_byte3 [string reverse [string replace [string replace $reversed_hex_el 0 3] 2 3]]
  set amended_reversed_hex_el_byte4 [string reverse [string replace $reversed_hex_el 0 5]]
  return $amended_reversed_hex_el_byte1$amended_reversed_hex_el_byte2$amended_reversed_hex_el_byte3$amended_reversed_hex_el_byte4
}


proc convert_decimal_string_to_ascii {decimal_string} {
  # string is handled as halfword in Parcel, as ASCII code is byte, thus needs to add a suffix here
  set suffix 00
  # in Parcel, string read handling is byte alignment
  if {[string length $decimal_string]%2 == 0} {
    set pad 00000000
  } else {
    set pad 0000
  }
  # convert input string param from decimal to ASCII format and concat them into a single string
  set remaining_string $decimal_string
  set response_string ""
  while {[string length $remaining_string] > 0} {
    set handling_string [string replace $remaining_string 1 [string length $remaining_string]]
    set response_string $response_string[format %.2x [scan $handling_string %c]]$suffix
    set remaining_string [string replace $remaining_string 0 0]
  }
  return $response_string$pad
}

proc hexNum2Dec { hex } {
    scan $hex %x v
    return $v
}

proc createUint8Array { hex_str_val } {
    return "[format %.8x [expr [string length $hex_str_val] / 2]]$hex_str_val"
}

proc createUint16Array { hex_str_val } {
    return "[format %.8x [expr [string length $hex_str_val] / 4]]$hex_str_val"
}

proc createUint32Array { hex_str_val } {
    return "[format %.8x [expr [string length $hex_str_val] / 8]]$hex_str_val"
}

proc getUint8Array { array } {
    set len_hex [string range $array 0 7]
    set len [hexNum2Dec $len_hex]
    set arrlen [expr [string length $array] - 8]
    if {[expr $arrlen % 2] != 0} {
        send_user "ERROR: uint8_array length mismatch (hex string length not even: $arrlen)\n"
        exit 1
    }
    if {$len != [expr $arrlen / 2]} {
        send_user "ERROR: uint8_array length mismatch ($len != [expr $arrlen / 2])\n"
        exit 1
    }
    return "[string range $array 8 [string length $array]]"
}

proc ascii_to_uint32_array { ascii_str } {
    set hex_str ""
    for {set i 0} {$i < [string length $ascii_str]} {incr i} {
        set hex_str "$hex_str[format %.8x [chr [string range $ascii_str $i $i]]]"
    }
    set hex_str "$hex_str[format %.8x 0]"
    return [createUint32Array $hex_str]
}

proc ascii2hex { ascii_str } {
    set hex_str ""
    for {set i 0} {$i < [string length $ascii_str]} {incr i} {
        set hex_str "$hex_str[format %.2x [chr [string range $ascii_str $i $i]]]"
    }
    return $hex_str
}

proc hex2ascii { hex_str } {
    set ascii_str ""
    #send_user "$hex_str\n"
    for {set i 0} {$i < [string length $hex_str]} {incr i; incr i} {
        set dec [hexNum2Dec [string range $hex_str $i [expr $i + 1]]]
        if {$dec == 0} {
            break
        }
        set ascii_str "$ascii_str[asc $dec]"
    }
    return $ascii_str
}

proc checkRegexp_Generic { {invert} {reg} {response ""} } {
    global test
    global verbose
    global progOutput
    global matchedProgOutput
    global unmatchedProgOutput

    set matched ""
    set unmatched ""
    set expression "($reg)(.*)"

    if { $response == "" } {
        set setGlobals 1
        set response $unmatchedProgOutput
    } else {
        set setGlobals 0
    }

    if { [regexp "$expression" "$response" dummy matched unmatched] } {
        if { $invert == 0 } {
            pass "$test"
        } else {
            fail "$test (match found)"
        }
        if { $setGlobals == 1 } {
            set matchedProgOutput $matched
            if { $invert == 0 } {
                set unmatchedProgOutput $unmatched
            }
        }
        if { $verbose > 0 } {
            if { $invert == 0 } {
                send_user "Pass: $test\n"
            }
            send_user "\tRegexp: \"$expression\"\n"
            send_user "\tMatched output: \"$matched\"\n"
        }
    } else {
        if { $invert == 0 } {
            fail "$test (bad match)"
            if { $verbose > 0 } {
                send_user "Fail: $test\n"
            }
        } else {
            pass "$test"
            if { $verbose > 0 } {
                send_user "Pass: $test\n"
            }
        }
        if { $setGlobals == 1 } {
            set matchedProgOutput ""
        }
        if { $verbose > 0 } {
            send_user "\tRegexp: \"$expression\"\n"
            send_user "\tUnmatched output: \"$response\"\n"
        }
    }
}

proc checkRegexp { {reg} {response ""} } {

    checkRegexp_Generic 0 "$reg" "$response"

}

proc checkRegexpInv { {reg} {response ""} } {

    checkRegexp_Generic 1 "$reg" "$response"

}

proc myEvalAndprint  { script unmatch_var match_var myregexp_var {pass_fail 1}} {
    global test
    global verbose

    upvar 1 $unmatch_var unmatch
    upvar 1 $match_var match
    upvar 1 $myregexp_var myregexp
    if { [uplevel 1 $script] } {
        if {$pass_fail} {
            pass "$test"

            if { $verbose > 0 } {
                send_user "Pass: $test\n"
                send_user "\tMatched output: \"$match\"\n"
            }
        }
    } else {
        if {$pass_fail} {
            fail "$test (bad match)"

            if { $verbose > 0 } {
                send_user "\tRegexp: \"$myregexp\"\n"
                send_user "\tUnmatched output: \"$unmatch\"\n"
            }
        }
    }
}

proc checkParamValue { param_name param_value msg {tag ""} } {
    set tag_str ""
    if {$tag != ""} {
        set tag_str "(\\\[$tag\\\])"
    } else {
        set tag_str "(\\\[\[0-9\]+\\\])?"
    }
    if {$param_value != ""} {
        if { [regexp "$param_name$tag_str=0x" $msg] } {
            checkRegexp "$param_name$tag_str=0x[format %x $param_value]\[,\}'\]|$param_name$tag_str=0x[format %x $param_value]$" $msg
        } else {
            checkRegexp "$param_name$tag_str=$param_value\[,\}'\]|$param_name$tag_str=$param_value$" $msg
        }
    }
    return [getParamValue $param_name $msg $tag]
}

proc checkParamValueInv { param_name param_value msg {tag ""} } {

    set tag_str ""
    if {$tag != ""} {
        set tag_str "(\\\[$tag\\\])"
    } else {
        set tag_str "(\\\[\[0-9\]+\\\])?"
    }

    if {$param_value == ""} {
        checkRegexpInv "$param_name$tag_str=" $msg
    } else {
        checkRegexpInv "$param_name$tag_str=$param_value" $msg
        if {[isinteger $param_value]} {
            checkRegexpInv "$param_name$tag_str=0x[format %x $param_value]" $msg
        }
    }
}

proc getBracketIndex { bstr {b "\{"} } {
    set pos [string first $b $bstr]
    if {$pos == -1} {
        return -1
    }
    return $pos
}

proc getNextBracketIndex {bstr pos {lb "\{"} {rb "\}"} } {
    set lpos [string first $lb [string range $bstr [expr $pos + 1] [string length $bstr]]]
    set rpos [string first $rb [string range $bstr [expr $pos + 1] [string length $bstr]]]

    if {$lpos == -1} {
        return [expr $pos + $rpos + 1]
    }

    if {$rpos == -1} {
        return [expr $pos + $lpos + 1]
    }

    if {$lpos < $rpos} {
        return [expr $pos + $lpos + 1]
    } else {
        return [expr $pos + $rpos + 1]
    }
}

proc charAtPos {str pos} {
    return [string range $str $pos $pos]
}

proc getFirstStringInMatchingBrackets { bstr {lb "\{"} {rb "\}"} } {
    set startPos [getBracketIndex $bstr $lb]
    if {$startPos == -1} {
        return $bstr
    }
    set pos $startPos
    set endPos [string length $bstr]
    set bcounter 1

    if {[getBracketIndex $bstr $rb] != -1} {
        while {true} {
            set pos [getNextBracketIndex $bstr $pos]
            if {$pos == -1} {
                break
            }
            if {[charAtPos $bstr $pos] == $lb} {
                incr bcounter
            } elseif {[charAtPos $bstr $pos] == $rb} {
                set bcounter [expr $bcounter - 1]
            } else {
                break
            }
            if {$bcounter == 0} {
                set endPos $pos
                break
            }
        }
    }

    return [string range $bstr [expr $startPos + 1] [expr $endPos - 1]]
}

proc getParamValue { param_name msg {tag ""} {fail 0} {compound 0}} {
    set value ""
    set tag_str ""
    if {$tag != ""} {
        set tag_str "(\\\[$tag\\\])"
    } else {
        set tag_str "(\\\[\[0-9\]+\\\])?"
    }
    if {$compound} {
        set myregexp "$param_name$tag_str=(\[^'\]*)"
    } else {
        set myregexp "$param_name$tag_str=(\[^,^\}'\]*)"
    }
    myEvalAndprint {regexp $myregexp $msg match tag value} msg match myregexp $fail
    if {$compound} {
        set value [getFirstStringInMatchingBrackets $value]
    }
    return $value
}

## Glue layer:
proc send_Cmd {request} {
    global spawn_id
    catch { send "$request\n" } tmp
    if [string match "*invalid spawn id*" $tmp] {
        send_user "ERROR: testprogram crashed? ('$tmp')\n"
        exit 1
    }
}

proc sync_Cmd {{syncpattern ""}} {
    global spawn_id
    global progOutput
    global matchedProgOutput
    global unmatchedProgOutput
    global prompt

    if { $syncpattern == "" } {
        set syncpattern "$prompt"
    }

    expect {
        -re "$syncpattern" {
        }
        timeout {
            send_user "ERROR: sync: timeout waiting for '$syncpattern'\n"
            exit 1
        }
    }

    set progOutput $expect_out(buffer)
    set unmatchedProgOutput $progOutput
    set matchedProgOutput ""
}

proc get_Msg { name } {
    global unmatchedProgOutput
    global matchedProgOutput
    global debug

    set msg ""
    set unmatched ""
    set myregexp "($name\[^\n\]*)(.*)"

    myEvalAndprint {regexp $myregexp \
                        $unmatchedProgOutput dummy msg unmatched } unmatchedProgOutput msg myregexp 0
    if {$debug} {
        send_user "*** $myregexp\n"
        send_user "*** matchedProgOutput: $matchedProgOutput\n"
        send_user "*** unmatchedProgOutput: $unmatchedProgOutput\n"
    }
    return $msg
}

proc check_Value { name value msg {tag ""} } {
    return [checkParamValue $name $value $msg $tag]
}

proc get_Value { name msg {tag ""} } {
    return [getParamValue $name $msg $tag]
}

proc get_CompoundValue { name msg {tag ""} } {
    return [getParamValue $name $msg $tag 0 1]
}

